plugins {
	id 'org.springframework.boot' version "$springVersion"
	id 'io.spring.dependency-management' version "$springDependencyManagementVersion"
	id "org.hidetake.swagger.generator" version "$swaggerGeneratorVersion" apply false
	id "org.sonarqube" version "$sonarVersion"
	id "io.gitlab.arturbosch.detekt" version "$detektVersion"
	id 'java'
}

apply from: 'plugins/swagger.gradle'
apply from: 'plugins/detekt.gradle'

group = 'com.scrum'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

sourceSets {
	main {
		java {
			srcDir file("${buildDir}/generated/source/")
			srcDir file("${buildDir}/swagger-code-server/src/main/java")
		}
		resources { srcDir 'src/main/resources' }
	}
	test {
		resources { srcDir 'src/test/resources' }
	}

	main.java.srcDirs += 'src/main/java'
	test.java.srcDirs += 'src/test/java'
}

repositories {
	mavenCentral()
}

ext {
	springFoxArtifacts = [
			'springfox-swagger2',
			'springfox-swagger-ui'
	]
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation "org.mapstruct:mapstruct:$mapstructVersion"
	implementation 'io.rest-assured:xml-path:4.5.0'
	implementation "io.rest-assured:json-path:$restAssuredVersion"

	annotationProcessor "org.mapstruct:mapstruct-processor:$mapstructVersion"

	runtimeOnly 'mysql:mysql-connector-java'

	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation "io.rest-assured:spring-mock-mvc:$restAssuredVersion"
	testImplementation "io.rest-assured:rest-assured:$restAssuredVersion"

	swaggerCodegen "io.swagger:swagger-codegen-cli:$swaggerCodeGenVersion"
	swaggerUI "org.webjars:swagger-ui:$swaggerUIVersion"

	springFoxArtifacts.each {
		artifact -> implementation "io.springfox:$artifact:$springFoxVersion"
	}
}

sonarqube {
	properties {
		property "sonar.projectKey", "rcdwoods_planning-and-retro"
		property "sonar.organization", "rcdwoods"
		property "sonar.host.url", "https://sonarcloud.io"
	}
}

test {
	useJUnitPlatform()
}

tasks.processResources {
	duplicatesStrategy = 'include'
}

tasks.processTestResources {
	duplicatesStrategy = 'include'
}